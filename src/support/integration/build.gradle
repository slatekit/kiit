buildscript {
    ext.kotlin_version = '1.8.22'
    ext.kiit_version = file('../../version.txt').text
    ext.kiit_version_beta = file('../../version-beta.txt').text

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "maven-publish"
     id "org.jetbrains.kotlin.jvm" // version "$kotlin_version"
}

apply from: '../../../build/gradle/gradle-utils.gradle'

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

java {
    withSourcesJar()
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/slatekit/kiit"
        credentials {
            username = System.getenv('KIIT_INSTALL_ACTOR')
            password = System.getenv('KIIT_INSTALL_TOKEN')
        }
    }
}

ext.kiitSetupViaBinary = System.getenv('KIIT_PROJECT_MODE') != 'sources'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
    implementation "com.googlecode.json-simple:json-simple:1.1"
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'org.threeten:threetenbp:1.3.8'

    // /* <kiit_local>
    if( kiitSetupViaBinary ) {
        implementation "dev.kiit:kiit-results:$kiit_version"
        implementation "dev.kiit:kiit-common:$kiit_version"
        implementation "dev.kiit:kiit-context:$kiit_version"
        implementation "dev.kiit:kiit-requests:$kiit_version"
        implementation "dev.kiit:kiit-meta:$kiit_version"
        implementation "dev.kiit:kiit-query:$kiit_version"
        implementation "dev.kiit:kiit-cli:$kiit_version"
        implementation "dev.kiit:kiit-entities:$kiit_version"
        implementation "dev.kiit:kiit-core:$kiit_version"
        implementation "dev.kiit:kiit-comms:$kiit_version"
        implementation "dev.kiit:kiit-cache:$kiit_version"
        implementation "dev.kiit:kiit-apis:$kiit_version"
        implementation "dev.kiit:kiit-jobs:$kiit_version"
        implementation "dev.kiit:kiit-policy:$kiit_version"
        implementation "dev.kiit:kiit-migrations:$kiit_version"
        implementation "dev.kiit:kiit-connectors-entities:$kiit_version"
    } else {
        // */
    implementation project(":kiit-result")
    implementation project(":kiit-common")
    implementation project(":kiit-context")
    implementation project(":kiit-requests")
    implementation project(":kiit-meta")
    implementation project(":kiit-query")
    implementation project(":kiit-cli")
    implementation project(":kiit-entities")
    implementation project(":kiit-core")
    implementation project(":kiit-policy")
    implementation project(":kiit-cache")
    implementation project(":kiit-apis")
    implementation project(":kiit-jobs")
    implementation project(":kiit-migrations")
    implementation project(":kiit-comms")
    implementation project(":connectors-entities")
    } //</kiit_local>
}

// ==================================================================
// Slate Kit Component Info
// ==================================================================
def kiitComponentId = 'integration'
def kiitComponentName = 'Integration'
def kiitComponentDesc = 'Kiit Integration: Integrates 2 or more slatekit components'
def kiitComponentVersion = ext.kiit_version


// ==================================================================
// Slate Kit Setup mode: defaults to maven vs loading project sources
// ==================================================================
task info {
    println('kiit.setup     : ' + System.getenv('KIIT_PROJECT_MODE'))
    println('kiit.maven     : ' + kiitSetupViaBinary)
    println('kiit.comp.id   : ' + kiitComponentId)
    println('kiit.comp.name : ' + kiitComponentName)
    println('kiit.comp.desc : ' + kiitComponentDesc)
    println('kiit.comp.vers : ' + kiitComponentVersion)
    println()
    println('project.name       : ' + project.name)
    println('project.path       : ' + project.path)
    println('project.desc       : ' + project.description)
    println('project.projectDir : ' + project.projectDir)
    println('project.buildDir   : ' + project.buildDir)
    println()
    println('build.commit       : ' + gitCommitId())
    println('build.branch       : ' + gitBranchName())
    println('build.date         : ' + getBuildDate())
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/slatekit/kiit"
            credentials {
                username = System.getenv("KIIT_PUBLISH_ACTOR")
                password = System.getenv("KIIT_PUBLISH_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            groupId 'dev.kiit'
            artifactId "kiit-${kiitComponentId}"
            version "${kiitComponentVersion}"
        }
    }
}

