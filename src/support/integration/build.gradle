buildscript {
    ext.kotlin_version = '1.6.20'
    ext.kiit_version = new File('../version.txt').text
    ext.kiit_version_beta = new File('../version-beta.txt').text

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "maven-publish"
     id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
}

apply from: '../../../build/gradle/gradle-utils.gradle'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/slatekit/kiit"
        credentials {
            username = System.getenv('KIIT_INSTALL_ACTOR')
            password = System.getenv('KIIT_INSTALL_TOKEN')
        }
    }
}

ext.kiitSetupViaBinary = System.getenv('KIIT_PROJECT_MODE') != 'sources'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
    compile "com.googlecode.json-simple:json-simple:1.1"
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'org.threeten:threetenbp:1.3.8'

    // /* <slatekit_local>
    if( kiitSetupViaBinary ) {
        compile "dev.kiit:results:$kiit_version"
        compile "dev.kiit:common:$kiit_version"
        compile "dev.kiit:context:$kiit_version"
        compile "dev.kiit:requests:$kiit_version"
        compile "dev.kiit:meta:$kiit_version"
        compile "dev.kiit:query:$kiit_version"
        compile "dev.kiit:cli:$kiit_version"
        compile "dev.kiit:entities:$kiit_version"
        compile "dev.kiit:core:$kiit_version"
        compile "dev.kiit:comms:$kiit_version"
        compile "dev.kiit:cache:$kiit_version"
        compile "dev.kiit:apis:$kiit_version"
        compile "dev.kiit:jobs:$kiit_version"
        compile "dev.kiit:policy:$kiit_version"
        compile "dev.kiit:migrations:$kiit_version"
        compile "dev.kiit:connectors-entities:$kiit_version"
    } else {
        // */
    compile project(":kiit-result")
    compile project(":kiit-common")
    compile project(":kiit-context")
    compile project(":kiit-requests")
    compile project(":kiit-meta")
    compile project(":kiit-query")
    compile project(":kiit-cli")
    compile project(":kiit-entities")
    compile project(":kiit-core")
    compile project(":kiit-policy")
    compile project(":kiit-cache")
    compile project(":kiit-apis")
    compile project(":kiit-jobs")
    compile project(":kiit-migrations")
    compile project(":kiit-comms")
    compile project(":connectors-entities")
    } //</slatekit_local>
}

// ==================================================================
// Slate Kit Component Info
// ==================================================================
def kiitComponentId = 'integration'
def kiitComponentName = 'Integration'
def kiitComponentDesc = 'Kiit Integration: Integrates 2 or more slatekit components'
def kiitComponentVersion = ext.kiit_version


// ==================================================================
// Slate Kit Setup mode: defaults to maven vs loading project sources
// ==================================================================
task info {
    println('slatekit.setup     : ' + System.getenv('KIIT_PROJECT_MODE'))
    println('slatekit.maven     : ' + kiitSetupViaBinary)
    println('slatekit.comp.id   : ' + kiitComponentId)
    println('slatekit.comp.name : ' + kiitComponentName)
    println('slatekit.comp.desc : ' + kiitComponentDesc)
    println('slatekit.comp.vers : ' + kiitComponentVersion)
    println()
    println('project.name       : ' + project.name)
    println('project.path       : ' + project.path)
    println('project.desc       : ' + project.description)
    println('project.projectDir : ' + project.projectDir)
    println('project.buildDir   : ' + project.buildDir)
    println()
    println('build.commit       : ' + gitCommitId())
    println('build.branch       : ' + gitBranchName())
    println('build.date         : ' + getBuildDate())
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/slatekit/kiit"
            credentials {
                username = System.getenv("KIIT_PUBLISH_ACTOR")
                password = System.getenv("KIIT_PUBLISH_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            groupId 'dev.kiit'
            artifactId "${kiitComponentId}"
            version "${kiitComponentVersion}"
        }
    }
}

