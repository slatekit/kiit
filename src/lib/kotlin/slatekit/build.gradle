apply from: '../../../../build/gradle/slatekit-common.gradle'

buildscript {
    ext.kotlin_version = '1.3.21'
    ext.slatekit_version = new File('../version.txt').text

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.3"
    }

}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'


// ==================================================================
// Slate Kit Component Info
// ==================================================================
def slatekitComponentId = 'exe'
def slatekitComponentName = 'Exe'
def slatekitComponentDesc = 'SlateKit Exe: CLI application'
def slatekitComponentVersion = getCommonVersion()


// ==================================================================
// Slate Kit Setup mode: defaults to maven vs loading project sources
// ==================================================================
ext.slatekitSetupViaMaven = System.getenv('SLATEKIT_SETUP_MODE') != 'sources'
task info {
    println('slatekit.setup     : ' + System.getenv('SLATEKIT_SETUP_MODE'))
    println('slatekit.maven     : ' + slatekitSetupViaMaven)
    println('slatekit.comp.id   : ' + slatekitComponentId)
    println('slatekit.comp.name : ' + slatekitComponentName)
    println('slatekit.comp.desc : ' + slatekitComponentDesc)
    println('slatekit.comp.vers : ' + slatekitComponentVersion)
    println()
    println('project.name       : ' + project.name)
    println('project.path       : ' + project.path)
    println('project.desc       : ' + project.description)
    println('project.projectDir : ' + project.projectDir)
    println('project.buildDir   : ' + project.buildDir)
    println()
    println('build.commit       : ' + gitCommitId())
    println('build.branch       : ' + gitBranchName())
    println('build.date         : ' + getBuildDate())
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.3'
        languageVersion = '1.3'
    }
}


repositories {
    jcenter()
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/codehelixinc/slatekit"
    }
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
    compile "mysql:mysql-connector-java:5.1.13"
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'org.threeten:threetenbp:1.3.8'

    /* <slatekit_local>
    if( slatekitSetupViaMaven ) {
        compile "com.slatekit:slatekit-results:$slatekit_version"
        compile "com.slatekit:slatekit-common:$slatekit_version"
        compile "com.slatekit:slatekit-app:$slatekit_version"
        compile "com.slatekit:slatekit-db:$slatekit_version"
        compile "com.slatekit:slatekit-meta:$slatekit_version"
        compile "com.slatekit:slatekit-cloud:$slatekit_version"
        compile "com.slatekit:slatekit-query:$slatekit_version"
        compile "com.slatekit:slatekit-entities:$slatekit_version"
        compile "com.slatekit:slatekit-orm:$slatekit_version"
        compile "com.slatekit:slatekit-core:$slatekit_version"
        compile "com.slatekit:slatekit-apis:$slatekit_version"
        compile "com.slatekit:slatekit-integration:$slatekit_version"
        compile "com.slatekit:slatekit-server:$slatekit_version"
        compile "com.slatekit:slatekit-providers:$slatekit_version"
        compile "com.slatekit:slatekit-jobs:$slatekit_version"
    } else {
        // */
        implementation project(":slatekit-result")
        implementation project(":slatekit-common")
        implementation project(":slatekit-app")
        implementation project(":slatekit-cli")
        implementation project(":slatekit-db")
        implementation project(":slatekit-meta")
        implementation project(":slatekit-query")
        implementation project(":slatekit-cloud")
        implementation project(":slatekit-entities")
        implementation project(":slatekit-orm")
        implementation project(":slatekit-core")
        implementation project(":slatekit-notifications")
        implementation project(":slatekit-apis")
        implementation project(":slatekit-integration")
        implementation project(":slatekit-server")
        implementation project(":slatekit-providers")
        implementation project(":slatekit-jobs")
        implementation project(":slatekit-generator")
        //compile project(":slatekit-support")
    // } //</slatekit_local>

    implementation project(":slatekit-examples")
    implementation project(":slatekit-tests")
}

// https://www.anexinet.com/blog/six-ways-to-package-a-java-application-with-gradle/
// gradle build distZip
mainClassName = 'slatekit.RunKt'

//sourceSets.main.resources.srcDirs = [ "src/" ]

jar {
    manifest {
        attributes(
                'Main-Class': 'slatekit.RunKt',
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

task copyAllJars(type: Copy) {
    dependsOn build
    into "lib"
    from configurations.runtime
}