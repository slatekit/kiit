apply from: '../../../../build/gradle/slatekit-common.gradle'

buildscript {
    ext.kotlin_version = '1.3.0'
    ext.slatekit_version = new File('../version.txt').text

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'


// ==================================================================
// Slate Kit Component Info
// ==================================================================
def slatekitComponentId = 'providers'
def slatekitComponentName = 'Providers'
def slatekitComponentDesc = 'SlateKit Providers: 3rd-party provider setup for logging, metrics'
def slatekitComponentVersion = getCommonVersion()


// ==================================================================
// Slate Kit Setup mode: defaults to maven vs loading project sources
// ==================================================================
ext.slatekitSetupViaMaven = System.getenv('SLATEKIT_SETUP_MODE') != 'sources'
task info {
    println('slatekit.setup     : ' + System.getenv('SLATEKIT_SETUP_MODE'))
    println('slatekit.maven     : ' + slatekitSetupViaMaven)
    println('slatekit.comp.id   : ' + slatekitComponentId)
    println('slatekit.comp.name : ' + slatekitComponentName)
    println('slatekit.comp.desc : ' + slatekitComponentDesc)
    println('slatekit.comp.vers : ' + slatekitComponentVersion)
    println()
    println('project.name       : ' + project.name)
    println('project.path       : ' + project.path)
    println('project.desc       : ' + project.description)
    println('project.projectDir : ' + project.projectDir)
    println('project.buildDir   : ' + project.buildDir)
    println()
    println('build.commit       : ' + gitCommitId())
    println('build.branch       : ' + gitBranchName())
    println('build.date         : ' + getBuildDate())
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.3'
        languageVersion = '1.3'
    }
}


repositories {
    jcenter()
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/codehelixinc/slatekit"
    }
}

dependencies {

    //compile group: 'org.slf4j'     , name: 'slf4j-api', version: '1.7.7'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    compile group: 'ch.qos.logback', name: 'logback-core'   , version: '1.1.2'
    compile group: 'org.logback-extensions', name: 'logback-ext-loggly'   , version: '0.1.2'

    // https://mvnrepository.com/artifact/io.dropwizard.metrics/metrics-core
    //compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.0'
    compile 'io.micrometer:micrometer-registry-datadog:latest.release'


    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.slatekit:slatekit-results:0.9.9"
    compile 'org.threeten:threetenbp:1.3.8'


    if( slatekitSetupViaMaven ) {
        compile "com.slatekit:slatekit-common:$slatekit_version"
        compile "com.slatekit:slatekit-meta:$slatekit_version"
        compile "com.slatekit:slatekit-entities:$slatekit_version"
        compile "com.slatekit:slatekit-core:$slatekit_version"
        compile "com.slatekit:slatekit-apis:$slatekit_version"
        compile "com.slatekit:slatekit-integration:$slatekit_version"
    } else {
        compile project(":slatekit-common")
        compile project(":slatekit-meta")
        compile project(":slatekit-entities")
        compile project(":slatekit-core")
        compile project(":slatekit-apis")
        compile project(":slatekit-integration")
    }
}

// ==================================================================
// BinTray Integration
// ==================================================================
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


// Maven packages require the sources/docs.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "Apache 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "codehelix"
            name "kishore reddy"
            email "kishore@codehelix.co"
        }
    }

    scm {
        url "https://github.com/code-helix/slatekit-${slatekitComponentId}"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.slatekit'
            artifactId "slatekit-${slatekitComponentId}"
            version "${slatekitComponentVersion}"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "${slatekitComponentDesc}")
                root.appendNode('name', "SlateKit ${slatekitComponentName}")
                root.appendNode('url', "https://github.com/code-helix/slatekit-${slatekitComponentId}")
                root.children().last() + pomConfig
            }
        }
    }
}

// gradle bintrayUpload
bintray {

    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('SLATEKIT_BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('SLATEKIT_BINTRAY_API_KEY')
    publish = true
    //configurations = ['archives']
    publications = ['mavenPublication']
    pkg {
        repo            = "slatekit"
        name            = "slatekit-${slatekitComponentId}"
        userOrg         = "codehelixinc"
        desc            = "${slatekitComponentDesc}"
        websiteUrl      = "http://www.slatekit.com/"
        issueTrackerUrl = "https://github.com/code-helix/slatekit-${slatekitComponentId}/issues"
        vcsUrl          = "https://github.com/code-helix/slatekit-${slatekitComponentId}"
        licenses        = ['Apache-2.0']
        publicDownloadNumbers = false
        version {
            name = "${slatekitComponentVersion}"
        }
    }
}

/* ==================================================================
*
* DEPLOY:
* 1. ensure bin-tray user/pswd env vars are set: bintrayUser, bintrayApiKey
* 2. replace the version number(s) in this file
* 3. cd into root directory
* 4. run gradle bintrayUpload
*
* LINKS:
* https://reflectoring.io/guide-publishing-to-bintray-with-gradle/
* https://stackoverflow.com/questions/52359686/configure-gradle-for-kotlin-with-java-1-7
*
* ===================================================================
*/
