buildscript {
    ext.kotlin_version = '1.3.72'
    ext.slatekit_version = new File('../version.txt').text
    ext.slatekit_version_beta = new File('../version-beta.txt').text

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
}

apply from: '../../../../build/gradle/slatekit-common.gradle'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/slatekit/slatekit"
        credentials {
            username = System.getenv('SLATEKIT_INSTALL_ACTOR')
            password = System.getenv('SLATEKIT_INSTALL_TOKEN')
        }
    }
}

ext.slatekitSetupViaBinary = System.getenv('SLATEKIT_PROJECT_MODE') != 'sources'

dependencies {

    //compile group: 'org.slf4j'     , name: 'slf4j-api', version: '1.7.7'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    compile group: 'ch.qos.logback', name: 'logback-core'   , version: '1.1.2'
    compile group: 'org.logback-extensions', name: 'logback-ext-loggly'   , version: '0.1.2'


    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.threeten:threetenbp:1.3.8'

    // /* <slatekit_local>
    if( slatekitSetupViaBinary ) {
        compile "com.slatekit:slatekit-results:$slatekit_version"
        compile "com.slatekit:slatekit-common:$slatekit_version"
        compile "com.slatekit:slatekit-actors:$slatekit_version"
        compile "com.slatekit:slatekit-core:$slatekit_version"
        compile "com.slatekit:slatekit-context:$slatekit_version"
        compile "com.slatekit:slatekit-apis:$slatekit_version"
        compile "com.slatekit:slatekit-jobs:$slatekit_version"
        compile "com.slatekit:slatekit-connectors-entities:$slatekit_version"
        compile "com.slatekit:slatekit-providers-aws:$slatekit_version"
    } else {
        // */
    compile project(":slatekit-result")
    compile project(":slatekit-common")
    compile project(":slatekit-actors")
    compile project(":slatekit-core")
    compile project(":slatekit-context")
    compile project(":slatekit-apis")
    compile project(":slatekit-jobs")
    compile project(":slatekit-connectors-entities")
    compile project(":slatekit-providers-aws")
    } //</slatekit_local>
}

// ==================================================================
// Slate Kit Component Info
// ==================================================================
def slatekitComponentId = 'connectors-jobs'
def slatekitComponentName = 'Connectors Jobs'
def slatekitComponentDesc = 'SlateKit Connectors: Connects the Jobs, APIs Core, modules'
def slatekitComponentVersion = ext.slatekit_version


// ==================================================================
// Slate Kit Setup mode: defaults to maven vs loading project sources
// ==================================================================
task info {
    println('slatekit.setup     : ' + System.getenv('SLATEKIT_PROJECT_MODE'))
    println('slatekit.maven     : ' + slatekitSetupViaBinary)
    println('slatekit.comp.id   : ' + slatekitComponentId)
    println('slatekit.comp.name : ' + slatekitComponentName)
    println('slatekit.comp.desc : ' + slatekitComponentDesc)
    println('slatekit.comp.vers : ' + slatekitComponentVersion)
    println()
    println('project.name       : ' + project.name)
    println('project.path       : ' + project.path)
    println('project.desc       : ' + project.description)
    println('project.projectDir : ' + project.projectDir)
    println('project.buildDir   : ' + project.buildDir)
    println()
    println('build.commit       : ' + gitCommitId())
    println('build.branch       : ' + gitBranchName())
    println('build.date         : ' + getBuildDate())
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/slatekit/slatekit"
            credentials {
                username = System.getenv("SLATEKIT_PUBLISH_ACTOR")
                password = System.getenv("SLATEKIT_PUBLISH_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            groupId 'com.slatekit'
            artifactId "slatekit-${slatekitComponentId}"
            version "${slatekitComponentVersion}"
        }
    }
}

