buildscript {
    ext.kotlin_version = '1.6.20'
    ext.ktor_version = '1.6.8'
    ext.kiit_version = file('../../version.txt').text
    ext.kiit_version_beta = file('../../version-beta.txt').text

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" // version "$kotlin_version"
}

apply from: '../../../build/gradle/gradle-utils.gradle'

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

java {
    withSourcesJar()
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/slatekit/kiit"
        credentials {
            username = System.getenv('KIIT_INSTALL_ACTOR')
            password = System.getenv('KIIT_INSTALL_TOKEN')
        }
    }
}

ext.kiitSetupViaBinary = System.getenv('KIIT_PROJECT_MODE') != 'sources'

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-metrics:$ktor_version"


    implementation "org.json:json:20201115"
    implementation "com.googlecode.json-simple:json-simple:1.1"
    //implementation 'com.sparkjava:spark-core:2.6.0'
    implementation 'org.threeten:threetenbp:1.3.8'
    implementation fileTree(dir: 'lib', include: '*.jar')

    /* <slatekit_local>
    if( kiitSetupViaBinary ) {
        implementation "dev.kiit:kiit-results:$kiit_version"
        implementation "dev.kiit:kiit-common:$kiit_version"
        implementation "dev.kiit:kiit-utils:$kiit_version"
        implementation "dev.kiit:kiit-requests:$kiit_version"
        implementation "dev.kiit:kiit-context:$kiit_version"
        implementation "dev.kiit:kiit-telemetry:$kiit_version"
        implementation "dev.kiit:kiit-apis:$kiit_version"
        implementation "dev.kiit:kiit-serialization:$kiit_version"
    } else {
        // */
    implementation project(":kiit-result")
    implementation project(":kiit-common")
    implementation project(":kiit-utils")
    implementation project(":kiit-requests")
    implementation project(":kiit-context")
    implementation project(":kiit-telemetry")
    implementation project(":kiit-apis")
    implementation project(":kiit-serialization")
    // } //</slatekit_local>
}

// ==================================================================
// Slate Kit Component Info
// ==================================================================
def kiitComponentId = 'server'
def kiitComponentName = 'Server'
def kiitComponentDesc = 'Kiit Server: Universal API Server'
def kiitComponentVersion = ext.kiit_version

// ==================================================================
// Slate Kit Setup mode: defaults to maven vs loading project sources
// ==================================================================
task info {
    println('slatekit.setup     : ' + System.getenv('KIIT_PROJECT_MODE'))
    println('slatekit.maven     : ' + kiitSetupViaBinary)
    println('slatekit.comp.id   : ' + kiitComponentId)
    println('slatekit.comp.name : ' + kiitComponentName)
    println('slatekit.comp.desc : ' + kiitComponentDesc)
    println('slatekit.comp.vers : ' + kiitComponentVersion)
    println()
    println('project.name       : ' + project.name)
    println('project.path       : ' + project.path)
    println('project.desc       : ' + project.description)
    println('project.projectDir : ' + project.projectDir)
    println('project.buildDir   : ' + project.buildDir)
    println()
    println('build.commit       : ' + gitCommitId())
    println('build.branch       : ' + gitBranchName())
    println('build.date         : ' + getBuildDate())
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/slatekit/kiit"
            credentials {
                username = System.getenv("KIIT_PUBLISH_ACTOR")
                password = System.getenv("KIIT_PUBLISH_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            groupId 'dev.kiit'
            artifactId "kiit-${kiitComponentId}"
            version "${kiitComponentVersion}"
        }
    }
}


