buildscript {
    ext.kotlin_version = '1.6.20'
    ext.kiit_version = file('../../version.txt').text
    ext.kiit_version_beta = file('../../version-beta.txt').text

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" // version "$kotlin_version"
    id("org.jlleitschuh.gradle.ktlint") version "10.3.0"
}

apply from: '../../../build/gradle/gradle-utils.gradle'

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

java {
    withSourcesJar()
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/slatekit/kiit"
        credentials {
            username = System.getenv('KIIT_INSTALL_ACTOR')
            password = System.getenv('KIIT_INSTALL_TOKEN')
        }
    }
}

ext.kiitSetupViaBinary = System.getenv('KIIT_PROJECT_MODE') != 'sources'

dependencies {

    //implementation group: 'org.slf4j'     , name: 'slf4j-api', version: '1.7.7'

    // https://mvnrepository.com/artifact/io.dropwizard.metrics/metrics-core
    //implementation group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.0'
    implementation 'io.micrometer:micrometer-registry-datadog:latest.release'


    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.threeten:threetenbp:1.3.8'

    // /* <slatekit_local>
    if( kiitSetupViaBinary ) {
        implementation "dev.kiit:kiit-results:$kiit_version"
        implementation "dev.kiit:kiit-common:$kiit_version"
        implementation "dev.kiit:kiit-telemetry:$kiit_version"
    } else {
        // */
    implementation project(":kiit-result")
    implementation project(":kiit-common")
    implementation project(":kiit-telemetry")
    } //</slatekit_local>
}

// ==================================================================
// Slate Kit Component Info
// ==================================================================
def kiitComponentId = 'providers-datadog'
def kiitComponentName = 'Providers DataDog'
def kiitComponentDesc = 'Kiit Providers: DataDog integration for metrics'
def kiitComponentVersion = ext.kiit_version

// ==================================================================
// Slate Kit Setup mode: defaults to maven vs loading project sources
// ==================================================================
ext.kiitSetupViaBinary = System.getenv('KIIT_PROJECT_MODE') != 'sources'
task info {
    println('slatekit.setup     : ' + System.getenv('KIIT_PROJECT_MODE'))
    println('slatekit.maven     : ' + kiitSetupViaBinary)
    println('slatekit.comp.id   : ' + kiitComponentId)
    println('slatekit.comp.name : ' + kiitComponentName)
    println('slatekit.comp.desc : ' + kiitComponentDesc)
    println('slatekit.comp.vers : ' + kiitComponentVersion)
    println()
    println('project.name       : ' + project.name)
    println('project.path       : ' + project.path)
    println('project.desc       : ' + project.description)
    println('project.projectDir : ' + project.projectDir)
    println('project.buildDir   : ' + project.buildDir)
    println()
    println('build.commit       : ' + gitCommitId())
    println('build.branch       : ' + gitBranchName())
    println('build.date         : ' + getBuildDate())
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/slatekit/kiit"
            credentials {
                username = System.getenv("KIIT_PUBLISH_ACTOR")
                password = System.getenv("KIIT_PUBLISH_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            groupId 'dev.kiit'
            artifactId "kiit-${kiitComponentId}"
            version "${kiitComponentVersion}"
        }
    }
}
